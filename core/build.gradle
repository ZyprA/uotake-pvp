plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("com.gradleup.shadow") version "9.3.0"
}

group = 'net.zypr'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven { url = uri("https://jitpack.io") }

}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    implementation("com.zaxxer:HikariCP:7.0.2")
    compileOnly("com.cjcrafter:weaponmechanics:4.1.5")
    compileOnly("com.cjcrafter:foliascheduler:0.7.2")
    compileOnly("com.cjcrafter:mechanicscore:4.1.5")
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.4.1'
    implementation("com.github.ZyprA:Fukurou:28892d8840")
}

shadowJar {
    relocate("com.github.ZyprA", "net.zypr.libs.com.github.ZyprA")
    relocate('com.zaxxer', 'net.zypr.libs.com.zaxxer')
}

tasks {
  runServer {
    minecraftVersion("1.21.4")
  }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
